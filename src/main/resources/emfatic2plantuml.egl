[%import "shared.eol";%]
@startuml
hide circle
skinparam roundCorner 0
hide empty methods
skinparam classBorderThickness 1
!pragma layout smetana
skinparam classFontName system-ui

[%for (e in EEnum.all){%]
class [%=e.name%] #[%=e.getEnumColor()%] {
[%for (l in e.eLiterals){%]
[%=l.name%]
[%}%]
}
[%}%]

[%for (c in EClass.all){%]
[%if(c.isAbstract){%]abstract [%}%]class [%=c.name%] #[%=c.getColor()%] {
	[%for (f in c.eStructuralFeatures){%]
	[%=f.getLabel()%]
	[%}%]
}
[%for (s in c.eSuperTypes){%]
[%=c.getEdge(s)%]
[%}%]
[%}%]

[%for (r in getUnidirectionalReferences().includingAll(getBidirectionalReferences())){%]
[%=r.getEdge()%]
[%}%]
[%for (a in getAttributeReferences()){%]
[%=a.getEdge()%]
[%}%]
@enduml
[*%out.toString().println();%*]
[%

operation EClass getEdge(supertype : EClass) {
	var direction = self.getAnnotationValue("diagram","inheritance.direction");
	if (direction == null) direction = "up";
	
	if (direction == "none") return "";

	var edge = "";

	switch (direction) {
		case "up": edge = supertype.name + " <|-- " + self.name;
		case "down": edge = self.name + " --|> " +  supertype.name;
		case "right": edge = self.name + " -|> " +  supertype.name;
		case "left": edge = supertype.name + " <|- " + self.name;
	}

	edge += ': "\t"';
	return edge;
}

operation EClass getExtends() {
	if (self.eSuperTypes.isEmpty()) return "";
	else return "extends " + self.eSuperTypes.name.concat(", ");
}

operation EAttribute getEdge() {
	var direction = self.getAnnotationValue("diagram","direction");
	if (direction == null) direction = "left";

	if (direction == "none") return "";

	var edge = "";

	switch (direction) {
		case "up": edge = self.eType.name + " " + "--" + " " + self.eContainer().name;
		case "down": edge = self.eContainer().name + " "  + "--" + " " + self.eType.name;
		case "right": edge = self.eContainer().name + " " + "-" + " " + self.eType.name;
		case "left": edge = self.eType.name + " " + "-" + " " +  self.eContainer().name;
	}

	edge += ': "\t"';
	return edge;
}

operation EReference getEdge() {
	
	var direction = self.getAnnotationValue("diagram","direction");

	if (direction == null and self.eOpposite.isDefined()) {
		var oppositeDirection = self.eOpposite.getAnnotationValue("diagram","direction");
		switch (oppositeDirection) {
			case "up": direction = "down";
			case "down": direction = "up";
			case "left": direction = "right";
			case "right": direction = "left";
			case "none": direction = "none";
		}
		if (direction == null and self.eOpposite.isContainment()) {
			direction = "up";
		}
	}

	if (direction == null) direction = self.isContainment ? "down" : "right";
	
	if (direction == "none") return "";

	var selfLeftEnd = self.isContainment ? "*" : (self.eOpposite.isDefined() ? "<" : "");
	var selfRightEnd = self.isContainment ? "*" : (self.eOpposite.isDefined() ? ">" : "");
	var otherLeftEnd = self.eOpposite.isDefined() ? (self.eOpposite.isContainment ? "*" : "<") : "<" ;
	var otherRightEnd = self.eOpposite.isDefined() ? (self.eOpposite.isContainment ? "*" : ">") : ">" ;
	
	var edge = "";
	
	switch (direction) {
		case "up": edge = self.eType.name + " " + otherLeftEnd + "--" + selfRightEnd + " " + self.eContainer().name;
		case "down": edge = self.eContainer().name + " " +  selfLeftEnd + "--" + otherRightEnd + " " +  self.eType.name;
		case "right": edge = self.eContainer().name + " " +  selfLeftEnd + "-" + otherRightEnd + " " +  self.eType.name;
		case "left": edge = self.eType.name + " " +  otherLeftEnd + "-" + selfRightEnd + " " +  self.eContainer().name;
	}
	
	edge += ': "\t"';
	return edge;
	
}

operation EStructuralFeature getLabel() {
	var label = self.name;
	if (self.isDerived()) label = "/" + label;
	if (self.eType.isDefined()) label += " : " + self.eType.name;
	label += self.getMultiplicityLabel();
	label += "  ";
	return label; 
}

operation EStructuralFeature getMultiplicityLabel() {
	var label = "";
	if (self.isMany) {
		label += "[";
		if (self.lowerBound <> 0) label += self.lowerBound + "..";
		if (self.upperBound == -1) {
			label += "*";
		}
		else {
			label += self.upperBound;
		}
		label += "]";
	}
	return label;
}

@cached
operation getUnidirectionalReferences() {
	var bidirectionalReferences = getBidirectionalReferences();
	return getVisibleReferences().excludingAll(bidirectionalReferences).
		excludingAll(bidirectionalReferences.eOpposite);
}

@cached
operation getBidirectionalReferences() {
	var references = getVisibleReferences();
	return references.select(r|r.eOpposite.isDefined() and references.indexOf(r) < references.indexOf(r.eOpposite));
}

@cached
operation getAttributeReferences() {
	return EAttribute.all.select(a|a.eType.isTypeOf(EEnum));
}

@cached
operation getVisibleReferences() { 
	return EReference.all;
}

operation EEnum getEnumColor() {
	// If a color is specified for the enumeration, use it
	var color = self.getAnnotationValue("diagram", "color");
	if (color != null) return color;

	// If the container package is monochrome use that colour
	var monochrome = self.eContainer().getMonochrome();
	
	// If monochrome is true, return a default color
	if (monochrome == "true") {
		return "floralwhite";
	}
	// Else if a non-false value is specified treat it as the selected color
	else if (monochrome <> null and monochrome <> "false") { 
		return monochrome;
	}
	// Else return a fixed color
	else {
		return "cornsilk";
	}
}
%]
